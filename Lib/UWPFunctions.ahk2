#Include ..\common\dllFactory.ahk2
#DllLoad "Propsys.dll"
;----------------------------------------------------------------------
; UWP Apps store their icons under a folder in WindowsApps
; Look for all the UWP installed apps and cache the best square logo
; Then later load that logo when needed for a particular app
; NOTE Limiation now: if we install a new UWP app, we have to
; restart Switcher to cache it's new logo
;----------------------------------------------------------------------
/*
   app.name = Short text name of app, e.g. Phone Link
   System.AppUserModel.PackageFamilyName = Microsoft.YourPhone_8wekyb3d8bbwe
   app.path/System.AppUserModel.ID = PackageFullName without the center _0x86_ stuff, Microsoft.YourPhone_8wekyb3d8bbwe!App (AUMID)
   System.AppUserModel.PackageFullName = full name, Microsoft.YourPhone_1.25032.82.0_x64__8wekyb3d8bbwe
   System.AppUserMode.PackageInstallPath = folder C:\Program Files\WindowsApps\Microsoft.YourPhone_1.25032.82.0_x64__8wekyb3d8bbwe
   PackageFamilyName: <PackageName>_<PublisherId>, e.g. Microsoft.Windows.Photos_8wekyb3d8bbwe
      PackageName The name of the app as specified in the manifest.
   PackageFullName: <PackageName>_<Version>_<Architecture>_<ResourceId>_<PublisherId>, e.g. Microsoft.Windows.Photos_2020.20090.1002.0_x64__8wekyb3d8bbwe
      Uniquely identifies a specific version and build of the app
*/
Class UWP {
   Static UWPIcons := A_ScriptDir . "\Icons\UWPIcons"
   Static nameMap := Map(), manMap := Map(), IDMap := Map()
   ; Cache any needed UWP icons on startup
   Static __New() {
      apps := 0
      For app in ComObject('Shell.Application').NameSpace('shell:AppsFolder').Items {
         ; Look for UWP Store Apps only
         If (app.Path ~= "!") {
            ; Store the AppUserModelId by the application display (icon) name
            AppID                  := StrSplit(app.Path, "!")[2] ; -> This can be used to find the Application Id                       = "xxx" match in manifest like we do for the executable now
            appManifest            := app.ExtendedProperty("System.AppUserModel.PackageInstallPath") . "\AppxManifest.xml" ; this works, get path to manifest file + AppxManifest.xml
            this.nameMap[app.Path] := app.Name ;
            this.manMap[app.Path]  := appManifest
            this.IDMap[app.Path]   := AppID
            ; debug(app.Name "->" appManifest)
            ; packageFullName := app.ExtendedProperty("System.AppUserModel.PackageFullName") ; this works
            ; Can use two below to find an icon in the manifest
            this.__cacheUWPLogo(app.Path)
            apps++
         }
      }
      debug("Checked a total of " apps " UWP icons from shell:AppsFolder")
   }
   ;----------------------------------------------------------------------
   ; Cache the UWPLogo if needed
   ; AUMID = <PackageFamilyName>!<ApplicationId>
   ; PackageFamilyName: This is the unique identifier for the app package,
   ; typically derived from the app's name and publisher.
   ; ApplicationId: This is the identifier for the specific application within the package.
   ;----------------------------------------------------------------------
   Static __cacheUWPLogo(AUMID) {

      AppName        := this.nameMap[AUMID]
      sourceManifest := this.manMap[AUMID]
      AppID          := this.IDMap[AUMID]
      logoTargetLoc  := this.UWPIcons
      targetFileName := logoTargetLoc "\" AppName ".png"
      ; Don't overwrite either png or ico files if they exist
      If (FileExist(logoTargetLoc "\" AppName ".png") OR FileExist(logoTargetLoc "\" AppName ".ico"))
         Return 1

      SplitPath(sourceManifest, &ManifestName, &dirname, &ext, &base)
      Text := FileRead(sourceManifest)
      ; Iterate through the desired icon sizes, smaller ones have less padding
      For num in [44, 71, 150, 310] {
         reString := 'sU)Id="' AppID '".+Square' num 'x' num 'Logo="(.+)"'
         if (pos := RegExMatch(Text, reString, &mArray)) {
            ; Will need to scan files using the match as a pattern
            SplitPath(mArray[1], , &dir, &ext, &base)
            logoFileWC := dirname "\" dir "\" base "*." ext
            ; Get the largest file we can
            If (FileExist(sourceFileName := getBiggestLogo(logoFileWC))) {
               ;----------------------------------------------------------------------
               ; If we have the right image file then copy the image over to a know location
               ; Later use imagemagick to convert to a ico file with multiple sizes
               ; Magick %%A -alpha set -trim -define icon:auto-resize="256,192,128,96,64,48,32,24,16" ICO\%%~nA.ico
               ;----------------------------------------------------------------------
               Try {
                  FileCopy(sourceFileName, targetFileName, 0)
                  dlog("Copied " sourceFileName)
                  dlog("    To " targetFileName)
                  Return targetFileName
               } Catch Error As e {
                  logError("Error Copying " sourceFileName)
               }
            } Else
               warning("Couldn't find icon using pattern " logoFileWC)
            Break
         } Else {
            debug(sourceManifest)
         }
      }
      ;----------------------------------------------------------------------
      ; Find the largest image that fits the path, better for resizing
      ;----------------------------------------------------------------------
      getBiggestLogo(patt) {
         LogoName := ""
         nfs := fs := 0
         ; debug("Pattern=" patt)
         Loop Files patt {
            ; Avoid alternate forms or high contracts versions
            If ( NOT (A_LoopFileName ~= "altform" OR A_LoopFileName ~= "contrast")) {
               nfs := FileGetSize(A_LoopFilePath)
               ; debug("  Checking " A_LoopFileName ", size=" nfs)
               If nfs > fs {
                  LogoName := A_LoopFilePath
                  fs := nfs
               }
            }
         }
         ; debug(" Using " LogoName ", size=" fs)
         Return LogoName
      }
   }

   /**
    * Return the Common Name for some app
    * @param hWnd Handle of UWP app to get info on
    * @returns {String} UWP Short App Name
    */
   Static AppName(hWnd) {
      ; https://learn.microsoft.com/en-us/windows/win32/properties/props-system-appusermodel-id
      Static PKEY_AppUserModel_ID := "{9F4C2855-9F79-4B39-A8D0-E1D42DE1D5F3} 5" ; AppID
      ; one time cache
      ; Get the AppID property directly, it is an ATOM so get the corresponding string
      If (Prop := GetProp(hWnd, PKEY_AppUserModel_ID)) {
         ; BUG - Files app is returning a 0 for property
         sz := VarSetStrCapacity(&AUMID, 256)
         rl := DllCall("Kernel32\GlobalGetAtomName", "Ptr", Prop, "Str", AUMID, "Int", sz)
         Return this.nameMap.Has(AUMID) ? this.nameMap[AUMID] : WinGetProcessName(hWnd)
      } Else If (WinExist(hWnd)) {
         warning("Failed to return property for '" WinGetProcessName(hWnd) "'")
         Return WinGetProcessName(hWnd)
      } Else {
         Return ""
      }
   }
}
dlog("Include File Loaded")
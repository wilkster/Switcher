#Include ..\common\dllFactory.ahk2
#DllLoad "Propsys.dll"
;----------------------------------------------------------------------
; UWP Apps store their icons under a folder in WindowsApps
; Look for all the UWP installed apps and cache the best square logo
; Then later load that logo when needed for a particular app
; NOTE Limiation now: if we install a new UWP app, we have to
; restart Switcher to cache it's new logo
;----------------------------------------------------------------------
/*
   app.name = Short text name of app, e.g. Phone Link
   System.AppUserModel.PackageFamilyName = Microsoft.YourPhone_8wekyb3d8bbwe
   app.path/System.AppUserModel.ID = PackageFullName without the center _0x86_ stuff, Microsoft.YourPhone_8wekyb3d8bbwe!App (AUMID)
   System.AppUserModel.PackageFullName = full name, Microsoft.YourPhone_1.25032.82.0_x64__8wekyb3d8bbwe
   System.AppUserMode.PackageInstallPath = folder C:\Program Files\WindowsApps\Microsoft.YourPhone_1.25032.82.0_x64__8wekyb3d8bbwe
   PackageFamilyName: <PackageName>_<PublisherId>, e.g. Microsoft.Windows.Photos_8wekyb3d8bbwe
      PackageName The name of the app as specified in the manifest.
   PackageFullName: <PackageName>_<Version>_<Architecture>_<ResourceId>_<PublisherId>, e.g. Microsoft.Windows.Photos_2020.20090.1002.0_x64__8wekyb3d8bbwe
      Uniquely identifies a specific version and build of the app
*/
Class UWP {
   Static UWPIcons := A_ScriptDir . "\Icons\UWPIcons"
   Static AppMap := Map(), UPWFileMap := Map()
   ; Cache any needed UWP icons on startup
   Static __New() {
      if (NOT DirExist(this.UWPIcons))
         DirCreate(this.UWPIcons) 
      now := A_TickCount
      ; Iterating through this virtual apps folder is pretty slow (~500ms)
      For app in ComObject('Shell.Application').NameSpace('shell:AppsFolder').Items {
         ; Look for UWP Store Apps only, it taks ~500ms to iterate through all of the apps
         ; Non-store apps look like this: calibre->{6D809377-6AF0-444B-8957-A3773F02200E}\Calibre2\calibre.exe
         If (app.Path ~= "!") {
            ; Store the AppUserModelId by the application display (icon) name
            AppID               := StrSplit(app.Path, "!")[2] ; -> This can be used to find the Application Id                       = "xxx" match in manifest like we do for the executable now
            appManifest         := app.ExtendedProperty("System.AppUserModel.PackageInstallPath") . "\AppxManifest.xml" ; this works, get path to manifest file + AppxManifest.xml
            AUMID               := app.Path
            this.AppMap[AUMID]  := {Name : app.Name, Manifest : appManifest, ID : AppID}
            ; FullName := app.ExtendedProperty("System.AppUserModel.PackageFullName") ; available but not used, same as the folder name in windowsapps
            ; Find an appropriate icon in the manifest if needed
            this.CacheUWPLogoIfNeeded(AUMID)
         }
      }
      ;----------------------------------------------------------------------
      ; Cache the icon file locations now
      ;----------------------------------------------------------------------
      dLog("Checked a total of " this.AppMap.Count " UWP Apps from shell:AppsFolder in " A_TickCount - now " msec")
      now := A_TickCount
      Loop Files this.UWPIcons . "\*.*" {
         If (A_LoopFileExt ~= "i)ico|png") {
            SplitPath(A_LoopFileFullPath, , , , &baseName)
            this.UPWFileMap[baseName] := A_LoopFileFullPath
         }
      }
      dLog("Cached a total of " this.UPWFileMap.Count " UWP Icons from " this.UWPIcons  " in " A_TickCount - now " msec")

   }
   /**
    * Return the location of athe bitmap file for a UWP app
    * @param AppName AMUID for UWP App
    * @returns File name of the bitmap file for it (0 if not found)
    */
   Static AppToBMFile(AppName) {
      return this.UPWFileMap.Get(AppName, 0)
   }
   ;----------------------------------------------------------------------
   ; Cache the UWPLogo if needed
   ; AUMID = <PackageFamilyName>!<ApplicationId>
   ; PackageFamilyName: This is the unique identifier for the app package,
   ; typically derived from the app's name and publisher.
   ; ApplicationId: This is the identifier for the specific application within the package.
   ;----------------------------------------------------------------------
   Static CacheUWPLogoIfNeeded(AUMID) {

      AppObj         := this.AppMap[AUMID]
      logoTargetLoc  := this.UWPIcons
      targetFileName := logoTargetLoc "\" AppObj.Name ".png"
      ; Don't overwrite either png or ico files if they exist
      If (FileExist(logoTargetLoc "\" AppObj.Name ".png") OR FileExist(logoTargetLoc "\" AppObj.Name ".ico"))
         Return 1

      If (!FileExist(AppObj.Manifest)) {
         warning("Could not find manifest: " AppObj.Manifest)
         Return 0
      }
      SplitPath(AppObj.Manifest, &ManifestName, &dirname, &ext, &base)
      Text := FileRead(AppObj.Manifest)
      ; Iterate through the desired icon sizes, smaller ones have less padding
      For token in [44, 71, 150, 310, "SmallLogo", "Logo"] {
         ; Look for logo after AppID
         reString := IsNumber(token) ? 'sU)Id="' AppObj.ID '".+Square' token 'x' token 'Logo="(.+)"' : 'sU)Id="' AppObj.ID '".+' token '="(.+)"'
         if (pos := RegExMatch(Text, reString, &mArray)) {
            ; Will need to scan files using the match as a pattern
            SplitPath(mArray[1], , &dir, &ext, &base)
            logoFileWC := dirname "\" dir "\" base "*." ext
            ; Get the largest file we can
            If (FileExist(sourceFileName := getBiggestLogo(logoFileWC))) {
               ;----------------------------------------------------------------------
               ; If we have the right image file then copy the image over to a know location
               ; Later use imagemagick to convert to a ico file with multiple sizes
               ; Magick %%A -alpha set -trim -define icon:auto-resize="256,192,128,96,64,48,32,24,16" ICO\%%~nA.ico
               ;----------------------------------------------------------------------
               Try {
                  FileCopy(sourceFileName, targetFileName, 0)
                  dlog("Copied " sourceFileName)
                  dlog("    To " targetFileName)
                  Return targetFileName
               } Catch Error As e {
                  logError("Error Copying " sourceFileName)
               }
            } Else
               warning("Couldn't find icon using pattern " logoFileWC)
            Break
         } Else {
            debug("Tried " token " in " AppObj.Manifest)
         }
      }
      ;----------------------------------------------------------------------
      ; Find the largest image that fits the path, better for resizing
      ;----------------------------------------------------------------------
      getBiggestLogo(patt) {
         LogoPath := ""
         nfs := lfs := 0
         Loop Files patt {
            ; Avoid alternate forms or high contrast versions
            If ( NOT (A_LoopFileName ~= "altform|contrast|wide")) {
               nfs := FileGetSize(A_LoopFilePath)
               If nfs > lfs {
                  LogoPath := A_LoopFilePath
                  lfs := nfs
               }
            }
         }
         Return LogoPath
      }
   }
   /**
    * Return the Common Name for some UWP app from the window handle
    * @param hWnd Handle of UWP app to get info on
    * @returns {String} UWP Short App Name
    */
   Static AppName(hWnd) {
      ; https://learn.microsoft.com/en-us/windows/win32/properties/props-system-appusermodel-id
      Static PKEY_AppUserModel_ID := "{9F4C2855-9F79-4B39-A8D0-E1D42DE1D5F3} 5" ; AppID
      ; Get the AppID property directly, it is an ATOM so get the corresponding AppName string
      Try {
         If (Prop := GetProp(hWnd, PKEY_AppUserModel_ID)) {
            AUMID := GetGlobalAtomName(Prop)
            Return (AUMID AND this.AppMap.Has(AUMID)) ? this.AppMap[AUMID].Name : WinGetProcessName(hWnd)
         } Else If (WinExist(hWnd)) {
            warning("Failed to return property for '" WinGetTitle(hWnd) "'")
            Return WinGetProcessName(hWnd)
         } Else {
            Return ""
         }
      } Catch Error As e {
         if (e.Message ~= "denied")
            warning("Access denied for " hex(hWnd))
         else
            eLog(e, "Failed to get AppName:" A_LastError)
      }
      Return ""
   }
}
dlog("Include File Loaded")